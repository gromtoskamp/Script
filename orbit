//make throttle directly controllable.
set t to 1.
lock throttle to t.

//Kerbins G
set g to 9.8.

//make heading controllable.
set steer to heading(90, 90).
lock steering to steer.

//staging trigger.
set lockstaging to 0.
when maxthrust = 0 then {
    if lockstaging = 0 {
        print "Staging".
        stage.
        wait 0.1.
    }
    preserve.
}.

set next to 100.
when ship:velocity:surface:mag > next then {
    print "SPD: " + next.
    print "ALT: " + ship:altitude.
    print "APO: " + ship:apoapsis.
    set next to (next + 100).
    if (ship:velocity:surface:mag < 800) {
        preserve.
    }
}

//initial stage.
print "lets get to orbit!".
set state to "launch".
until state = "stop" {

    if state = "launch" {
        set correctedSpeed to ship:velocity:surface:mag - 50.

        if correctedSpeed < 0 {
            set correctedSpeed to 0.
        }.
        set down to 90 - (correctedSpeed/10).

        if down < 5 {
            set down to 5.
        }

        set steer to heading(90, down).

        if ship:apoapsis > 80000 {
            print "cutting throttle".
            set t to 0.
            print "coasting".
            set state to "coast".
        }
    }

    if state = "coast" {
        set steer to heading(90, 0).
        if ship:apoapsis < 80000 {
            set t to 1.
        }

        if ship:apoapsis > 80000 {
            set t to 0.
        }

        if eta:apoapsis < 5 {
            print "finetune".
            set state to "finetune".
        }
    }

    if state = "finetune" {
        set steer to heading(90, 0).
        if eta:apoapsis < 5 {
            set t to 1.
        }

        if eta:apoapsis > 5 {
            set t to 0.
        }

        if eta:apoapsis > 100 {
            set t to 1.
        }

        if ship:periapsis > 75000 {
            set state to "stop".
        }
    }
}
